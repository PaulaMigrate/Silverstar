IF
See([PC])
NumberOfTimesTalkedTo(0)
THEN
RESPONSE #100
	Dialog(Nearest([PC]))
END

IF
IfValidForPartyDialogue(Myself)
Global("Slyich","LOCALS",0)
See("DuegarClanChief")
!Dead("DuegarClanChief")
THEN
RESPONSE #100
SetGlobal("Slyich","LOCALS",1)
StartDialogNoSet(Myself)
END

IF
PartyHasItem("Misca2")
PartyHasItem("D#Silve1")
Global("SilverPoison","GLOBAL",0)
THEN
RESPONSE #100
TakePartyItem("D#Silve1")
DestroyItem("D#Silve1")
GiveItemCreate("D#Silve2","D#Silver",1,0,0)
SetGlobal("SilverPoison","GLOBAL",1)
AddSpecialAbility("D#Spoi1")
StartDialogNoSet(Myself)
END

IF
Global("SilWallace","GLOBAL",2)
PartyHasItem("Misca2")
PartyHasItem("D#Silve1")
Global("SilverPoison","GLOBAL",1)
THEN
RESPONSE #100
TakePartyItem("D#Silve1")
DestroyItem("D#Silve1")
DestroyItem("Misca2")
GiveItemCreate("D#Silve2","D#Silver",1,0,0)
SetGlobal("SilverPoison","GLOBAL",2)
AddSpecialAbility("D#Spoi1")
END

IF
PartyHasItem("D#Crybl1")
!HasItem("D#Crybl1",Myself)
Global("D#SilverSword","GLOBAL",1)
THEN
RESPONSE #100
TakePartyItem("D#Crybl1")
GiveItem("D#Crybl1","D#Silver")
StartDialogNoSet(Myself)
END

IF
Global("D#SilverSword","GLOBAL",1)
HasItem("D#Crybl1",Myself)
THEN
RESPONSE #100
StartDialogNoSet(Myself)
END

IF
!HasItem("D#Crybl1",Myself)
Global("D#SilverSword","GLOBAL",2)
THEN
RESPONSE #100
Dialog(Player1)
END

IF
Global("D#SilverSword","GLOBAL",3)
HasItem("D#Crybl1",Myself)
THEN
RESPONSE #100
SetGlobal("D#SilverSword","GLOBAL",2)
END

IF
Global("D#SilverSword","GLOBAL",3)
RealGlobalTimerExpired("Swordtime","LOCALS")
THEN
RESPONSE #100
Dialog(Player1)
END

IF
PartyRested()
IfValidForPartyDialogue(Myself)
See(Player1)
GlobalGT("SilverEvil","GLOBAL",0)
Global("SilDream1","GLOBAL",0)
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("SilDream1","GLOBAL",1)
SetGlobalTimer("Nightmare","GLOBAL",TWO_DAYS)
Wait(5)
StartDialogNoSet(Player1)
END

IF
PartyRested()
GlobalTimerExpired("Nightmare","GLOBAL")
IfValidForPartyDialogue(Myself)
See(Player1)
Global("SilDream1","GLOBAL",2)
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("SilDream1","GLOBAL",3)
SetGlobalTimer("Nightmare","GLOBAL",FIVE_DAYS)
Wait(5)
StartDialogNoSet(Player1)
END

IF
PartyRested()
GlobalTimerExpired("Nightmare","GLOBAL")
IfValidForPartyDialogue(Myself)
See(Player1)
Global("SilDream1","GLOBAL",4)
THEN
RESPONSE #100
SetGlobal("SilDream1","GLOBAL",5)
Wait(5)
StartDialogNoSet(Player1)
END

IF
Global("Silverinitbanter","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("Silverinitbanter","LOCALS",1)
SetGlobalTimer("Siltalk","GLOBAL",THREE_DAYS)
END

IF
Global("#Silveryy1","GLOBAL",0)
GlobalTimerExpired("Siltalk","GLOBAL")
See(Player1)
IfValidForPartyDialogue(Myself)
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("#Silveryy1","GLOBAL",1)
Dialog(Player1)
END

IF
Global("OneMoTime","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("OneMoTime","GLOBAL",2)
SetGlobalTimer("TellPC","GLOBAL",ONE_DAY)
END

IF
GlobalTimerExpired("TellPC","GLOBAL")
Global("OneMoTime","GLOBAL",2)
IfValidForPartyDialogue(Myself)
See(Player1)
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("#Silveryy1","GLOBAL",1)
SetGlobal("OneMoTime","GLOBAL",3)
StartDialogNoSet(Player1)
END

IF
Global("SawKhalid","AR0603",1)
Global("D#SawKhalid","LOCALS",0)
InParty("Imoen2")
Delay(20)
THEN
RESPONSE #100
SetGlobal("D#SawKhalid","LOCALS",1)
Dialog("Imoen2")
END

IF
Global("SilverEvil","GLOBAL",10)
Global("Silvermorph","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("Silvernext","GLOBAL",ONE_MINUTE)
SetGlobal("Silvermorph","GLOBAL",1)
END

IF
Global("Silvermorph","GLOBAL",1)
RealGlobalTimerExpired("Silvernext","GLOBAL")
THEN
RESPONSE #100
RealSetGlobalTimer("Silvernext","GLOBAL",ONE_MINUTE)
CreateVisualEffectObject("SPFLSRIN",Myself)
Recoil()
SetGlobal("Silvermorph","GLOBAL",2)
END

IF
Global("Silvermorph","GLOBAL",2)
RealGlobalTimerExpired("Silvernext","GLOBAL")
THEN
RESPONSE #100
RealSetGlobalTimer("Silvernext","GLOBAL",ONE_MINUTE)
SetGlobal("Silvermorph","GLOBAL",3)
CreateVisualEffectObject("SPFIREPI",Myself)
Recoil()
END

IF
Global("Silvermorph","GLOBAL",3)
RealGlobalTimerExpired("Silvernext","GLOBAL")
THEN
RESPONSE #100
CreateVisualEffectObject("SPFIREPI",Myself)
Recoil()
SetGlobal("Silvermorph","GLOBAL",4)
CreateVisualEffectObject("SPFIREPI",Myself)
RealSetGlobalTimer("Silvernext","GLOBAL",TWO_MINUTES)
Dialog(Nearest([PC]))
END

IF
Global("Silvermorph","GLOBAL",5)
RealGlobalTimerExpired("Silvernext","GLOBAL")
THEN
RESPONSE #100
RealSetGlobalTimer("Silvernext","GLOBAL",ONE_MINUTE)
SetGlobal("Silvermorph","GLOBAL",6)
CreateVisualEffectObject("SPCLOUD1",Myself)
CreateVisualEffectObject("SPCLOUD1",Player1)
Wait(2)
CreateVisualEffectObject("SPCLOUD1",Myself)
Dialog(Nearest([PC]))
END

IF
Global("Silvermorph","GLOBAL",7)
THEN
RESPONSE #100
Kill("D#Silver")
CreateVisualEffectObject("SPCLOUD1",Myself)
CreateCreatureObjectEffect("D#SBansh","","D#Silver")
LeaveParty()
SetGlobal("Silvermorph","GLOBAL",8)
END

IF
IfValidForPartyDialogue("Yoshimo")
!Dead("Yoshimo")
Global("YoshTalk","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("YoshTalk","LOCALS",1)
SetGlobalTimer("ImSoPretty","GLOBAL",THREE_HOURS)
END

IF
GlobalTimerExpired("ImSoPretty","GLOBAL")
IfValidForPartyDialogue("Yoshimo")
See("Yoshimo")
!Dead("Yoshimo")
CombatCounter(0)
Global("SilverYoshi","LOCALS",0)
THEN
RESPONSE #100
Interact("Yoshimo")  // Yoshimo
END

IF
Global("SilRenfeld","GLOBAL",0)
CombatCounter(0)
Dead("rethug01")
Dead("rethug02")
Dead("rethug03")
Dead("rethug04")
PartyHasItem("MISC5F")
THEN
RESPONSE #100
Wait(5)
StartDialogNoSet(Player1)
END

IF
IfValidForPartyDialogue(Myself)
AreaCheck("AR1404")
Global("ShadeForest","GLOBAL",0)
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("ShadeForest","GLOBAL",1)
StartDialogNoSet(Myself)
END

IF
Global("NaliaKeepPlot","GLOBAL",1)
IfValidForPartyDialogue(Myself)
IfValidForPartyDialogue("Nalia")
!Dead("Nalia")
CombatCounter(0)
Global("DNaliaTalk","LOCALS",0)
OR(2)
AreaCheck("AR0400")
AreaCheck("AR0300")
THEN
RESPONSE #100
SetGlobal("DNaliaTalk","LOCALS",1)
Interact("Nalia")
END

IF
AreaCheck("AR0603")
Global("SThief1","LOCALS",0)
Dead("ISHADMT1")
Delay(5)
THEN
RESPONSE #100
PlaySound("IRENIC04")
Wait(8)
StartDialogNoSet(Myself)
END

IF
PartyHasItem("MISC5L")
!HasItem("MISC5L",Myself)
See(Player1)
Global("DLittle","GLOBAL",1)
CombatCounter(0)
THEN
RESPONSE #100
TakePartyItem("MISC5L")
GiveItem("MISC5L","D#Silver")
StartDialogNoSet(Player1)
END

IF
Global("DLittle","GLOBAL",1)
HasItem("MISC5L",Myself)
See(Player1)
CombatCounter(0)
THEN
RESPONSE #100
StartDialogNoSet(Player1)
END

IF
IfValidForPartyDialogue(Myself)
Dead("GRREVEN")
HasItem("MISC5L",Myself)
Global("DropBear","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("DropBear","GLOBAL",1)
DropItem("MISC5L",[-1.-1])
StartDialogNoSet(Myself)
END

IF
AreaCheck("AR0301")
Global("Maevar","LOCALS",0)
Delay(3)
THEN
RESPONSE #100
SetGlobal("Maevar","LOCALS",1)
StartDialogNoSet(Myself)
END

IF
IfValidForPartyDialogue(Myself)
See("Edwin")
Gender("Edwin",FEMALE)
Global("D#Sextalk","GLOBAL",0)
Global("SilverEdwina","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("D#Sextalk","GLOBAL",1)
SetGlobalTimer("EdwinaTwit","GLOBAL",FOUR_HOURS)
END

IF
GlobalTimerExpired("EdwinaTwit","GLOBAL")
IfValidForPartyDialogue(Myself)
GlobalLT("SilverEdwina","GLOBAL",2)
See("Edwin")
Gender("Edwin",FEMALE)
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("SilverEdwina","GLOBAL",2)
Interact("Edwin")
END

IF
PartyRested()
Global("HurtAerie","GLOBAL",0)
Global("SiverMazzy2","GLOBAL",0)
IfValidForPartyDialogue(Myself)
See("Mazzy")
See("Aerie")
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("HurtAerie","GLOBAL",1)
SetGlobal("SilverMazzy2","GLOBAL",1)
MoveToObject("Aerie")
Interact("Mazzy")
END

IF
IfValidForPartyDialogue(Myself)
Global("#Silveryy1","GLOBAL",2)
Global("SilverEdwina","GLOBAL",3)
Global("Tale2","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("Tale2","GLOBAL",1)
SetGlobalTimer("SobStory2","GLOBAL",FIVE_DAYS)
END

IF
GlobalTimerExpired("SobStory2","GLOBAL")
IfValidForPartyDialogue(Myself)
See(Player1)
Global("Tale2","GLOBAL",1)
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("Tale2","GLOBAL",2)
SetGlobal("#Silveryy1","GLOBAL",3)
StartDialogNoSet(Player1)
END

IF
Global("EdwinPoof","GLOBAL",1)
Global("SilverPissed","GLOBAL",0)
IfValidForPartyDialogue(Myself)
IfValidForPartyDialogue("Edwin")
THEN
RESPONSE #100
SetGlobal("SilverPissed","GLOBAL",1)
SetGlobalTimer("EdwinGyp","GLOBAL",THREE_DAYS)
END

IF
GlobalTimerExpired("EdwinGyp","GLOBAL")
IfValidForPartyDialogue(Myself)
IfValidForPartyDialogue("Edwin")
!Dead("Edwin")
Global("SilverPissed","GLOBAL",1)
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("SilverPissed","GLOBAL",2)
SetGlobal("EdwinPoof","GLOBAL",2)
Dialog("Edwin")
END

IF
AreaCheck("AR0307")
See("Booter")
!Dead("Booter")
Global("Booter","LOCALS",0)
Delay(3)
THEN
RESPONSE #100
SetGlobal("Booter","LOCALS",1)
StartDialogNoSet(Myself)
END

IF
Global("Underdark","GLOBAL",0)
AreaCheck("AR2100")
IfValidForPartyDialogue("Edwin")
!Dead("Edwin")
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("Underdark","GLOBAL",1)
Dialog("Edwin")
END

IF
See("Solaufein")
!Dead("Solaufein")
Global("SeeSola","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("SeeSola","LOCALS",1)
SetGlobalTimer("SilSola1","Global",THREE_HOURS)
END

IF
See("Solaufein")
!Dead("Solaufein")
GlobalTimerExpired("SilSola1","GLOBAL")
Global("SilSola","GLOBAL",0)
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("SilSola","GLOBAL",1)
Dialog("Solaufein")  // Solaufein
END

IF
IfValidForPartyDialogue(Myself)
IfValidForPartyDialogue("Haerdalis")  // Haer'Dalis
Global("SilHaerFight","GLOBAL",1)
THEN
RESPONSE #100
SetDialog("")
LeaveParty()
EquipMostDamagingMelee()
SpellNoDec(Myself,ASSASSIN_POISON)
ChangeAIScript("",DEFAULT)
ChangeAIScript("useitem",GENERAL)
Attack("Haerdalis")
END

IF
Global("SilHaerFight","GLOBAL",2)
See("Haerdalis")
!Dead("Haerdalis")
HPLT(Myself,30)
THEN
RESPONSE #100
SetGlobal("SilHaerFight","GLOBAL",3)
EscapeAreaMove("AR0020",672,604,4)
END

IF
Global("IrenTalk","LOCALS",1)
OR(2)
AreaCheck("AR0313")
AreaCheck("AR0509")
THEN
RESPONSE #100
StartDialogNoSet(Player1)
END

